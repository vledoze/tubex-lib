# ==================================================================
#  tubex-ode - cmake configuration file
# ==================================================================


list(APPEND SRC ${CMAKE_CURRENT_SOURCE_DIR}/tubex_capd2tubex.cpp
                ${CMAKE_CURRENT_SOURCE_DIR}/tubex_capd2tubex.h
                ${CMAKE_CURRENT_SOURCE_DIR}/tubex_TubeVectorODE.h
                ${CMAKE_CURRENT_SOURCE_DIR}/tubex_TubeVectorODE.cpp)

################################################################################
# Create the target for libtubex-ode
################################################################################

  add_library(tubex-ode ${SRC})
  target_compile_options(tubex-ode PUBLIC ${TUBEX_CFLAGS})

  # todo: find a clean way to access tubex header files?
  set(TUBEX_HEADERS_DIR ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
  target_include_directories(tubex-ode PUBLIC ${TUBEX_HEADERS_DIR}
                                               ${CMAKE_CURRENT_SOURCE_DIR})
  target_link_libraries(tubex-ode PUBLIC Ibex::ibex tubex)


################################################################################
# Installation of libtubex-ode files
################################################################################

# Getting header files from sources

  foreach(srcfile ${SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND HDR ${srcfile})
      # Copying header files for other Tubex libraries compiled in the same time
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)
    endif()
  endforeach()

# Generates a tubex-ode.h file

  set(TUBEX_3RD_ODE_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/tubex-ode.h)
  file(WRITE ${TUBEX_3RD_ODE_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${TUBEX_3RD_ODE_MAIN_HEADER} "#ifndef __TUBEX_3RD_ODE_H__\n#define __TUBEX_3RD_ODE_H__\n\n")
  foreach(header_path ${HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${TUBEX_3RD_ODE_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(APPEND ${TUBEX_3RD_ODE_MAIN_HEADER} "\n#endif \n")
  file(COPY ${TUBEX_3RD_ODE_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../../include)

# Install files in system directories
install(TARGETS tubex-ode DESTINATION ${CMAKE_INSTALL_LIBDIR})
install(FILES ${HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/tubex-ode)
install(FILES ${TUBEX_3RD_ODE_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/tubex-ode)

