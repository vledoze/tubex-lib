# ==================================================================
#  tubex-rob - cmake configuration file
# ==================================================================

  list(APPEND SRC ${CMAKE_CURRENT_SOURCE_DIR}/graphics/tubex_VIBesFigMap.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/graphics/tubex_VIBesFigMap.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/objects/tubex_Beacon.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/objects/tubex_Beacon.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/loops/tubex_TPlane.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/loops/tubex_TPlane.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/tubex_DataLoaderLissajous.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/tubex_DataLoaderRedermor.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/tubex_DataLoader.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/tubex_DataLoaderRedermor.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/tubex_DataLoader.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/data/tubex_DataLoaderLissajous.h
                  ${CMAKE_CURRENT_SOURCE_DIR}/contractors/tubex_CtcConstell.cpp
                  ${CMAKE_CURRENT_SOURCE_DIR}/contractors/tubex_CtcConstell.h
                  )


################################################################################
# Create the target for libtubex-rob
################################################################################

  add_library(tubex-rob ${SRC})
  target_compile_options(tubex-rob PUBLIC ${TUBEX_CFLAGS})

  # todo: find a clean way to access tubex header files?
  set(TUBEX_HEADERS_DIR ${CMAKE_CURRENT_BINARY_DIR}/../../include)
  target_include_directories(tubex-rob PUBLIC ${TUBEX_HEADERS_DIR}
                                              ${CMAKE_CURRENT_SOURCE_DIR}/graphics
                                              ${CMAKE_CURRENT_SOURCE_DIR}/objects
                                              ${CMAKE_CURRENT_SOURCE_DIR}/loops
                                              ${CMAKE_CURRENT_SOURCE_DIR}/data
                                              ${CMAKE_CURRENT_SOURCE_DIR}/contractors)
  target_link_libraries(tubex-rob PUBLIC Ibex::ibex tubex)


################################################################################
# Installation of libtubex-rob files
################################################################################

# Getting header files from sources

  foreach(srcfile ${SRC})
    if(srcfile MATCHES "\\.h$" OR srcfile MATCHES "\\.hpp$")
      list(APPEND HDR ${srcfile})
      # Copying header files for other Tubex libraries compiled in the same time
      file(COPY ${srcfile} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)
    endif()
  endforeach()

# Generating a tubex-rob.h file

  set(TUBEX_ROB_MAIN_HEADER ${CMAKE_CURRENT_BINARY_DIR}/tubex-rob.h)
  file(WRITE ${TUBEX_ROB_MAIN_HEADER} "/* This file is generated by CMake */\n\n")
  file(APPEND ${TUBEX_ROB_MAIN_HEADER} "#ifndef __TUBEX_ROB_H__\n#define __TUBEX_ROB_H__\n\n")
  foreach(header_path ${HDR})
    get_filename_component(header_name ${header_path} NAME)
    file(APPEND ${TUBEX_ROB_MAIN_HEADER} "#include <${header_name}>\n")
  endforeach()
  file(APPEND ${TUBEX_ROB_MAIN_HEADER} "\n#endif /* __TUBEX_ROB_H__ */\n")
  file(COPY ${TUBEX_ROB_MAIN_HEADER} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/../../include)

# Install files in system directories

  install(TARGETS tubex-rob DESTINATION ${CMAKE_INSTALL_LIBDIR})
  install(FILES ${HDR} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/tubex-rob)
  install(FILES ${TUBEX_ROB_MAIN_HEADER} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/tubex-rob)